include: "rules/common.smk"
include: "rules/convert.smk"
include: "rules/derep.smk"
include: "rules/swarm.smk"


(SAMPLES,) = glob_wildcards("reads/final/{sample}.fastq")


rule all:
    input:
        "data/swarm_stats.tsv",
        "data/swarm_cluster_representatives.fna",


rule stats:
    input:
        expand("reads/final/{sample}.swarm_stats_pretty.tsv", sample=SAMPLES),
    output:
        "data/swarm_stats.tsv",
    shell:
        """
        qsv cat rowskey --group fstem --group-name sample --output {output} {input}
        sed -i "s/\\.swarm_stats_pretty//" {output}
        """


rule representatives:
    input:
        expand("reads/final/{sample}.cluster_representatives.fna", sample=SAMPLES),
    output:
        "data/swarm_cluster_representatives.fna",
    shell:
        "seqkit rmdup --quiet --by-seq {input}"
        " | seqkit replace --pattern ';.+' --replacement ''"
        " | seqkit sort --quiet --line-width 0"
        " > {output}"
