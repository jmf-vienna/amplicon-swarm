include: "rules/common.smk"
include: "rules/convert.smk"
include: "rules/derep.smk"
include: "rules/swarm.smk"


(SAMPLES,) = glob_wildcards("reads/final/{sample}.fastq")


rule all:
    input:
        "data/reads.tsv",
        "data/sequences.tsv",
        "data/pooled_swarm_stats.tsv",
        "data/pooled_swarm_cluster_members.tsv",
        "data/solitary_swarm_stats.tsv",
        "data/solitary_swarm_cluster_members.tsv",


rule fasta_header_to_tsv:
    input:
        expand("reads/final/{sample}.fna", sample=SAMPLES),
    output:
        "data/reads.tsv",
    shell:
        """
        echo "sha1\tsample\texpected_errors\tlength" > {output}
        cat {input} | grep '^>' | tr ';' '\\t' | sed -e 's/^>//' -e 's/[a-z]\\+=//g' >> {output}
        """


rule fasta_to_tsv:
    input:
        "reads/final/all.dereplicated.fna",
    output:
        "data/sequences.tsv",
    shell:
        """
        echo "sha1\tsequence" > {output}
        seqkit fx2tab --no-qual {input} | sed "s/;size=[0-9]\\+//" | sort >> {output}
        """


rule pool_fasta:
    input:
        expand("reads/final/{sample}.fna", sample=SAMPLES),
    output:
        temp("reads/final/all.fna"),
    shell:
        """
        cat {input} > {output}
        """


rule pooled_stats:
    input:
        "reads/final/all.swarm_stats_pretty.tsv",
    output:
        "data/pooled_swarm_stats.tsv",
    shell:
        """
        csvsort --columns feature {input} | csvformat --out-tabs > {output}
        """


rule pooled_cluster_members:
    input:
        "reads/final/all.clusters.txt",
    output:
        "data/pooled_swarm_cluster_members.tsv",
    shell:
        """
        echo "seed\tmembers" > {output}
        cat {input} >> {output}
        sed -i \
            -e "s/;size=[0-9]\\+\\( \\|$\\)/\\t/" \
            -e "s/;size=[0-9]\\+//g" \
            {output}
        """


rule solitary_stats:
    input:
        expand("reads/final/{sample}.swarm_stats_pretty.tsv", sample=SAMPLES),
    output:
        "data/solitary_swarm_stats.tsv",
    shell:
        """
        qsv cat rowskey --group fstem --group-name sample --output {output} {input}
        sed -i "s/\\.swarm_stats_pretty//" {output}
        """


rule solitary_swarm_stats_without_singletons:
    input:
        "data/solitary_swarm_stats.tsv",
    output:
        "data/filtered_solitary_swarm_stats.tsv",
    shell:
        """
        qsv search --invert-match --select count "^1$" --output {output} {input}
        """


rule solitary_swarm_stats_without_singletons_ids:
    input:
        "data/filtered_solitary_swarm_stats.tsv",
    output:
        temp("data/solitary_swarm_non_singletons.tsv"),
    shell:
        """
        qsv select feature {input} | qsv sort --unique | qsv behead --output {output}
        """


rule solitary_representatives:
    input:
        expand("reads/final/{sample}.cluster_representatives.fna", sample=SAMPLES),
    output:
        "data/solitary_swarm_cluster_representatives.fna",
    shell:
        "seqkit rmdup --quiet --by-seq {input}"
        " | seqkit replace --pattern ';.+' --replacement ''"
        " | seqkit sort --quiet --line-width 0"
        " > {output}"


rule solitary_swarm_cluster_representatives_without_singletons:
    input:
        fasta="data/solitary_swarm_cluster_representatives.fna",
        ids="data/solitary_swarm_non_singletons.tsv",
    output:
        fasta="data/filtered_solitary_swarm_cluster_representatives.fna",
        index=temp("data/solitary_swarm_cluster_representatives.fna.fai"),
    shell:
        "seqkit faidx --quiet --line-width 0 --infile-list {input.ids} {input.fasta} > {output.fasta}"


rule solitary_metrics:
    input:
        "data/solitary_swarm_stats.tsv",
    output:
        "data/solitary_swarm_metrics.tsv",
    shell:
        """
        R --slave -e '
            readr::read_tsv("{input}", show_col_types = FALSE) |>
            dplyr::group_by(sample) |>
            dplyr::summarise(count = sum(count)) |>
            tibble::add_column(
                tool = "swarm/JMF-flavor",
                phase = "with singletons",
                .before = 1L
            ) |>
            readr::write_tsv("{output}")
        '
        """


rule solitary_cluster_members:
    input:
        expand("reads/final/{sample}.clusters.txt", sample=SAMPLES),
    output:
        "data/solitary_swarm_cluster_members.tsv",
    shell:
        """
        echo "sample\tseed\tmembers" > {output}
        awk 'BEGIN {{ OFS="\\t" }} {{ print FILENAME , $0 }}' {input} >> {output}
        sed -i \
            -e "s/^reads\\/final\\///" \
            -e "s/\\.clusters\\.txt//" \
            -e "s/;size=[0-9]\\+\\( \\|$\\)/\\t/" \
            -e "s/;size=[0-9]\\+//g" \
            {output}
        """
